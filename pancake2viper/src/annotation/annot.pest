WHITESPACE = _{ " " | "\t" | NEWLINE }

integer = @{ ASCII_DIGIT+ }
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" )* }

type = _{ int_t | bool_t | iarray_t }
    int_t = { "Int" }
    bool_t = { "Bool" }
    iarray_t = { "IArray" }
decl = { ident ~ ":" ~ type }

int_lit = @{ "-"? ~ integer}

bin_op = _{ add | sub | mul | div | modulo | imp | iff | viper_eq | viper_neq | pancake_eq | pancake_neq | gte | gt | lte | lt | bool_or | bool_and }
    add = { "+" }
    sub = { "-" }
    mul = { "*" }
    div = { "/" }
    modulo = { "%" }
    imp = { "==>" }
    iff = { "<==>" }
    viper_eq = { "===" }
    viper_neq = { "!==" }
    pancake_eq = { "==" }
    pancake_neq = { "!=" }
    gt = { ">" }
    gte = { ">=" }
    lt = { "<" }
    lte = { "<=" }
    bool_and = { "&&" }
    bool_or = { "||" }

expr = !{ prefix? ~ primary ~ postfix? ~ (infix ~ prefix? ~ primary ~ postfix?)* }
    infix = _{ bin_op }
    prefix = _{ neg | minus }
        neg = { "!" }
        minus = { "-" }
    
    postfix = _{ field_acc }
        field_acc = @{ ("." ~ field_idx)+ }
            field_idx = !{ integer }

    primary = _{ "(" ~ expr ~ ")" | int_lit | quantified | acc_pred | f_call | heap | field_acc | ident }

        quantified = { (forall | exists) ~ decl ~ ("," ~ decl)* ~ "::" ~ triggers ~ expr }
            forall = { "forall" }
            exists = { "exists" }
            triggers = { "{" ~ expr ~ ("," ~ expr)* ~ "}" | "" }

        acc_pred = {"acc" ~ "(" ~ expr ~ (("," ~ perm) | "") ~ ")" }
            perm = _{ perm_write | perm_read | perm_wildcard | perm_frac | perm_var }
                perm_write = { "write" }
                perm_read = { "read" }
                perm_wildcard = { "wildcard" }
                perm_frac = { integer ~ "/" ~ integer }
                perm_var = { ident }

        f_call = {ident ~ "(" ~ (expr ~ ("," ~ expr)* | "") ~ ")" }
        heap = { "heap[" ~ expr ~ "]" }


annotation = {"@"? ~ WHITESPACE* ~ annotation_keyword ~ expr ~ WHITESPACE* ~ "@"? }
    annotation_keyword = _{ pre | post | invariant | assertion | inhale | exhale }
        pre = { "requires" }
        post = { "ensures" }
        invariant = { "invariant" }
        assertion = { "assert" }
        inhale = { "inhale" }
        exhale = { "exhale" }

// TODO: change "/*" ~ "@" to "/*@" when Pancake parsing fixed
predicate = { "/*" ~ "@" ~ "predicate" ~ ident ~ "(" ~ pred_args ~ ")" ~ pred_body ~ "@"? ~ "*/" }
    pred_args = { (decl ~ ("," ~ decl)*) | "" }
    pred_body = { ("{" ~ expr ~ "}") | "" }
